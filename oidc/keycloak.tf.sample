terraform {
  required_providers {
    keycloak = {
      source  = "mrparkers/keycloak"
      version = "3.6.0"
    }
  }
}
# configure keycloak provider
provider "keycloak" {
  client_id                = "admin-cli"
  username                 = "admin"
  password                 = "admin@12345678"
  url                      = "https://fqdnOfKeycloakServer"
  tls_insecure_skip_verify = true
}
locals {
  realm_id = "master"
  groups   = ["kube-view-only", "kube-dev", "kube-admin"]
  user_groups = {
    user-view-only = ["kube-view-only"]
    user-dev   = ["kube-dev"]
    user-admin = ["kube-admin"]
  }
}
# create groups
resource "keycloak_group" "groups" {
  for_each = toset(local.groups)
  realm_id = local.realm_id
  name     = each.key
}
# create users
resource "keycloak_user" "users" {
  for_each       = local.user_groups
  realm_id       = local.realm_id
  username       = each.key
  enabled        = true
  email          = "${each.key}@emailDomainName"
  email_verified = true
  first_name     = each.key
  last_name      = each.key
  initial_password {
    value = each.key
  }
}
# configure use groups membership
resource "keycloak_user_groups" "user_groups" {
  for_each  = local.user_groups
  realm_id  = local.realm_id
  user_id   = keycloak_user.users[each.key].id
  group_ids = [for g in each.value : keycloak_group.groups[g].id]
}
# create groups openid client scope
resource "keycloak_openid_client_scope" "groups" {
  realm_id               = local.realm_id
  name                   = "groups"
  include_in_token_scope = true
  gui_order              = 1
}
resource "keycloak_openid_group_membership_protocol_mapper" "groups" {
  realm_id        = local.realm_id
  client_scope_id = keycloak_openid_client_scope.groups.id
  name            = "groups"
  claim_name      = "groups"
  full_path       = false
}
# create kube openid client
# Although separate clients can be created for each app., we are using the same client for all web apps
# You may need to add an additional redirect url http://localhost:8000 if kubelogin is being used
resource "keycloak_openid_client" "kube" {
  realm_id                     = local.realm_id
  client_id                    = "kube"
  name                         = "kube"
  enabled                      = true
  access_type                  = "CONFIDENTIAL"
  client_secret                = "kube-client-secret"
  standard_flow_enabled                     = true
  implicit_flow_enabled                     = false
  direct_access_grants_enabled              = true
  service_accounts_enabled                  = true
  valid_redirect_uris = [
    "https://gitlab.oidc.thecloudgarage.com:10443/*",
    "https://portainer.oidc.thecloudgarage.com:11443/*",
    "https://argocd.oidc.thecloudgarage.com/*"
   ]
}
# configure kube openid client default scopes
resource "keycloak_openid_client_default_scopes" "kube" {
  realm_id  = local.realm_id
  client_id = keycloak_openid_client.kube.id
  default_scopes = [
    "profile",
    "email",
    "groups",
    "roles",
    "web-origins",
    keycloak_openid_client_scope.groups.name,
  ]
}

# create openid client for argocd cli which is grpc based
resource "keycloak_openid_client" "argocdcligrpc" {
  realm_id                     = local.realm_id
  client_id                    = "argocdcligrpc"
  name                         = "argocdcligrpc"
  enabled                      = true
  access_type                  = "PUBLIC"
  standard_flow_enabled                     = true
  implicit_flow_enabled                     = false
  valid_redirect_uris = [
    "http://localhost:8085/*"
   ]
  pkce_code_challenge_method = "S256"
}
# configure kube openid client default scopes
resource "keycloak_openid_client_default_scopes" "argocdcligrpc" {
  realm_id  = local.realm_id
  client_id = keycloak_openid_client.argocdcligrpc.id
  default_scopes = [
    "profile",
    "email",
    "groups",
    "roles",
    "web-origins",
    keycloak_openid_client_scope.groups.name,
  ]
}

# output openid client secret
output "client-secret" {
  value     = keycloak_openid_client.kube.client_secret
  sensitive = true
}
